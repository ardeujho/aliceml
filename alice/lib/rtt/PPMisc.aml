(*
 * Author:
 *   Andreas Rossberg <rossberg@ps.uni-sb.de>
 *
 * Copyright:
 *   Andreas Rossberg, 2001-2005
 *
 * Last change:
 *   $Date$ by $Author$
 *   $Revision$
 *)

(*
 * Miscellaneous pretty printing helpers
 *)

import structure PrettyPrint from "../utility/PrettyPrint"
import signature PP_MISC     from "PP_MISC-sig"


structure PPMisc :> PP_MISC =
struct
    (* Import *)

    open PrettyPrint

    infixr ^^ ^/^


    (* Some PP combinators *)

    val nestDepth = 3
    val nest = nest nestDepth

    fun isShort s = String.size s < nestDepth

    fun contain (l,r) doc = text l ^^ fbox(below doc) ^^ text r
    val quote = contain("`","'")
    val paren = contain("(",")")
    val brace = contain("{","}")
    val brack = contain("[","]")

    fun apply(doc, docs) = fbox(nest(doc ^/^ List.foldr op^/^ empty docs))
    fun applyShort(doc, docs) =
	fbox(nest(doc ^^ text " " ^^ List.foldr op^/^ empty docs))
    fun indent doc = nest(break ^^ doc)
    fun par docs   = fbox(List.foldr op^/^ empty docs)
    fun textpar ss = fbox(List.foldr (fn(s,doc) => text s ^/^ doc) empty ss)

    fun ppCommaList ppX   []    = empty
      | ppCommaList ppX   [x]   = ppX x
      | ppCommaList ppX (x::xs) = ppX x ^^ text "," ^/^ ppCommaList ppX xs

    fun ppStarList ppX   []     = empty
      | ppStarList ppX   [x]    = ppX x
      | ppStarList ppX (x::xs)  = ppX x ^^ text " *" ^/^ ppStarList ppX xs

    fun ppSeqPrec ppXPrec n []  = empty
      | ppSeqPrec ppXPrec n [x] = ppXPrec n x
      | ppSeqPrec ppXPrec n  xs = paren(below(ppCommaList (ppXPrec 0) xs))

    fun ppSeq ppX = ppSeqPrec (fn _ => ppX) 0
end
