(*
 * Authors:
 *   Sebastian Germesin <germi@ps.uni-sb.de>
 *
 * Copyright:
 *   Sebastian Germesin, 2004
 *
 * Last change:
 *   $Date$ by $Author$
 *   $Revision$
 *)

import signature TESTSUITE from "../../src/TESTSUITE-sig"

import structure Random    from "x-alice:/lib/utility/Random"
import structure Gen       from "x-alice:/lib/test/Gen"
import structure Test      from "../../src/Test"

structure TestCharArray :> TESTSUITE =
struct

    val randomize = Gen.randomize

    fun fromInt x = if x > 255 then Char.chr 255 else Char.chr x

    fun testMaxLen () =
	Test.test (fn x => x = (0xfffff0 - 1) * 4) CharArray.maxLen

    fun testSub () =
	let
	    fun t (a, i) = if i < 0 orelse CharArray.length a <= i 
			       then NONE 
			   else SOME (List.nth (CharArray.toList a, i))
			       
	    fun t' x = let
			   val r = t x
		       in
			   CharArray.sub x = valOf r
			    handle Subscript => Option.isNone r
		       end
	in
	    Gen.try 100 (Gen.pair (Gen.charArray Gen.char, Gen.int))
	      (Test.test t')
	end

    fun testUpdate () = 
	let
	    fun t (a, i, x) = if i < 0 orelse CharArray.length a <= i
				  then NONE 
			      else SOME (CharArray.sub (a, i) before 
					 CharArray.update (a, i, x))
	    fun t' (a, i, x) = let
				   val r = t (a, i, x)
			       in
				   CharArray.sub (a, i) = x
				    handle Subscript => Option.isNone r
			       end
	in
	    Gen.try 100 (Gen.triple (Gen.charArray Gen.char, Gen.int, 
				     Gen.char))
	       (Test.test t')
	end

    fun testLength () =
	let
	    fun t (a, i) = (CharArray.sub (a, i); 
			    t (a, i + 1)) handle Subscript => i
	in
	    Gen.try 100 (Gen.charArray Gen.char)
	      (Test.test (fn a => t (a, 0) = CharArray.length a))
	end
				

    fun testToList () =
	let
	    fun t' (a, i, xs) = if i >= CharArray.length a 
				  then List.rev xs
			      else t' (a, i + 1, (CharArray.sub (a, i))::xs)

	    fun t a = t' (a, 0, []) = CharArray.toList a
	in
	    Gen.try 100 (Gen.charArray Gen.char) (Test.test t)
	end

    fun testArray () = 
	let
	    fun t' (xs, x, i) = List.all (fn a => a = x) xs andalso
			     List.length xs = i

	    fun t (i, x) = t' (CharArray.toList (CharArray.array (i, x)), x, i) 
			      handle Size => i < 0
	in
	    Gen.try 100 (Gen.pair (Gen.int, Gen.char))
	       (Test.test  t)
	end

    fun testVector () =
	let
	    fun t a = (CharVector.fromList (CharArray.toList a) = 
		       CharArray.vector a)
		          handle Size => CharArray.length a > 
					 CharVector.maxLen 
	in 
	    Gen.try 100 (Gen.charArray Gen.char) (Test.test t)
	end

    fun testToVector () =
	let
	    fun t a = (CharVector.fromList (CharArray.toList a) = 
		       CharArray.vector a)
		          handle Size => CharArray.length a > 
					 CharVector.maxLen 
	in 
	    Gen.try 100 (Gen.charArray Gen.char) (Test.test t)
	end

    fun testFromList () =
	let
	    fun t'' ([]   , a, i) = a
	      | t'' (x::xs, a, i) = (CharArray.update (a, i, x); 
				     t'' (xs, a, i - 1))

	    fun t' xs = let
			    val size = List.length xs
			    val null = fromInt 0
			in 
			    if size > CharArray.maxLen 
				then NONE
			    else SOME (t'' (List.rev xs, 
					    CharArray.array (size, null), 
					    size - 1))
			end
			    
	    fun t xs = let
			   val r = t' xs
		       in
			   CharArray.equal Char.equal (CharArray.fromList xs, 
						  valOf (t' xs))
			     handle Size => Option.isNone r
		       end

	    fun h xs = (CharArray.fromList xs; true)
		         handle Size => List.length xs > CharArray.maxLen
	in
	    Gen.try 100 (Gen.list Gen.char) (Test.test t)
	end
	    
    fun testTabulate () =
	let
	    fun t (n, f) = CharArray.toList (CharArray.tabulate (n, f)) =
		           List.tabulate (n, f)
			    handle Size => n < 0 orelse CharArray.maxLen < n

	    fun t' x = fromInt x
	in
	    Gen.try 100 Gen.int (Test.test (fn x => t (x, t')))
	end

    fun testContains () =
	let
	    fun t (a, x) = CharArray.contains Char.equal a x =
		           List.contains (CharArray.toList a) x
	in
	    Gen.try 100 (Gen.pair (Gen.charArray Gen.char, Gen.char)) 
		    (Test.test t)
	end

    fun testNotContains () =
	let
	    fun t (a, x) = CharArray.notContains Char.equal a x = 
		           Bool.not (CharArray.contains Char.equal a x)
	in
	    Gen.try 100 (Gen.pair (Gen.charArray Gen.char, Gen.char)) 
		    (Test.test t)
	end

    fun testSort () =
	let
	    fun t f a = List.sort f (CharArray.toList a) =
		        CharArray.toList (CharArray.sort f a; a)
	in
	    Gen.try 100 (Gen.charArray Gen.char) 
	       (Test.test (fn a => t Char.compare a))
	end

    fun testIsSorted () =
	let
	    fun t f a = CharArray.isSorted f a =
		        List.isSorted f (CharArray.toList a)
	in
	    Gen.try 100 (Gen.charArray Gen.char) 
	       (Test.test (fn a => t Char.compare a))
	end

    fun testCollate () =
	let
	    fun t f (a1, a2) = 
		List.collate f (CharArray.toList a1, 
				CharArray.toList a2) =
		CharArray.collate f (a1, a2)
	in
	    Gen.try 100 (Gen.pair (Gen.charArray Gen.char, Gen.charArray Gen.char))
	       (Test.test (fn x => t Char.compare x))
	end

    fun testEqual () =
	let
	    fun t f (a1, a2) = CharArray.equal f (a1, a2) =
		               List.equal f (CharArray.toList a1, 
					     CharArray.toList a2)
	in
	    Gen.try 100 (Gen.pair (Gen.charArray Gen.char, 
				   Gen.charArray Gen.char))
	        (Test.test (fn x => t Char.equal x))
	end

    fun testSwap () =
	let
	    fun t (ar, a, b) = 
	               let
	               	  val r = if a < 0 orelse b < 0 orelse
	                             a >= CharArray.length ar orelse
	                             b >= CharArray.length ar then NONE 
				  else SOME ()
                          fun h () = (CharArray.sub (ar, a), 
				      CharArray.sub (ar, b))
		       in
			   if Option.isSome r then 
			      let
				  val (abef, bbef) = h ()
			      in
				  (CharArray.swap (ar, a, b); 
				   abef = CharArray.sub (ar, b) andalso
				   bbef = CharArray.sub (ar, a))
				     handle Subscript => false
			      end
			   else (CharArray.swap (ar, a, b); false) 
	                              handle Subscript => true
		       end
	in
	    Gen.try 100 (Gen.triple (Gen.charArray Gen.char, Gen.int, Gen.int))
	       (Test.test t)
	end

    fun testRev () =
	let
	    fun t a = List.rev (CharArray.toList a) =
		      CharArray.toList (CharArray.rev a; a)
	in
	    Gen.try 100 (Gen.charArray Gen.char) (Test.test t)
	end

    fun testCopy () =
	let
	    fun t' (src, dst, di) i = if i = CharArray.length src then ()
				      else (CharArray.update (dst, di + i, 
							  CharArray.sub (src,
									  i));
					    t' (src, dst, di) (i + 1))
	    
	    fun t {src, dst, di} = if di < 0 orelse 
		                      CharArray.length dst < 
				      di + CharArray.length src 
				       then NONE
				   else SOME (t' (src, dst, di) 0)
	in
	    Gen.try 100 (Gen.triple (Gen.charArray Gen.char, 
				      Gen.charArray Gen.char,
				      Gen.int))
	    (Test.test (fn (src, dst, i) => 
			let
			 val src1 = CharArray.fromList (CharArray.toList src)
			 val src2 = CharArray.fromList (CharArray.toList src)
			 val dst1 = CharArray.fromList (CharArray.toList dst)
			 val dst2 = CharArray.fromList (CharArray.toList dst)
			 val r    = t {src = src2, dst = dst2, di = i}
			in
			    (CharArray.copy {src = src1, dst = dst1, di = i};
			     CharArray.equal Char.equal (dst1, dst2))
			     handle Subscript => Option.isNone r
			end))
	end

    fun testCopyVec () =
	let
	    fun t' (src, dst, di) i = if i = CharVector.length src then ()
				      else (CharArray.update (dst, di + i, 
							 CharVector.sub (src,
									  i));
					    t' (src, dst, di) (i + 1))
	    
	    fun t {src, dst, di} = if di < 0 orelse 
		                     CharArray.length dst < 
				     di + CharVector.length src 
				       then NONE
				   else SOME (t' (src, dst, di) 0)
	in
	    Gen.try 100 (Gen.triple (Gen.charArray Gen.char, 
				     Gen.charArray Gen.char,
				     Gen.int))
	    (Test.test (fn (src, dst, i) => 
			let
			val src1 = CharVector.fromList (CharArray.toList src)
			val src2 = CharVector.fromList (CharArray.toList src)
			val dst1 = CharArray.fromList (CharArray.toList dst)
			val dst2 = CharArray.fromList (CharArray.toList dst)
				   
			val r    = t {src = src2, dst = dst2, di = i}
			in
			    (CharArray.copyVec {src = src1, dst = dst1, 
						 di = i};
			     CharArray.equal Char.equal (dst1, dst2))
			     handle Subscript => Option.isNone r
			end))
	end

    fun testAll () =
	let
	    fun t f arr = List.all f (CharArray.toList arr)

	    fun f x = Char.> (x, fromInt 0)
	in
	    Gen.try 100 (Gen.charArray Gen.char) 
	     (Test.test (fn a => CharArray.all f a = t f a))
	end

    fun testExists () =
	let
	    fun t f arr = List.exists f (CharArray.toList arr)

	    fun f x = Char.> (x, fromInt 0)
	in
	    Gen.try 100 (Gen.charArray Gen.char)
	     (Test.test (fn a => CharArray.exists f a = t f a))
	end

    fun testFind () =
	let
	    fun t f arr = List.find f (CharArray.toList arr)

	    fun f x = Char.> (x, fromInt 0)
	in
	    Gen.try 100 (Gen.charArray Gen.char)
	     (Test.test (fn a => CharArray.find f a = t f a))
	end

    fun testAlli () =
	let
	    fun t f arr = List.alli f (CharArray.toList arr)

	    fun f (i, x) = (fromInt i) = x
	in
	    Gen.try 100 (Gen.charArray Gen.char) 
	     (Test.test (fn a => CharArray.alli f a = t f a))
	end

    fun testExistsi () =
	let
	    fun t f arr = List.existsi f (CharArray.toList arr)
		
	    fun f (i, x) = (fromInt i) = x
	in
	    Gen.try 100 (Gen.charArray Gen.char)
	     (Test.test (fn a => CharArray.existsi f a = t f a))
	end

    fun testFindi () =
	let
	    fun t f arr = List.findi f (CharArray.toList arr)
		
	    fun f (i, x) = (fromInt i) = x
	in
	    Gen.try 100 (Gen.charArray Gen.char)
	     (Test.test (fn a => CharArray.findi f a = t f a))
	end

    fun mkTestModify t f m =
	let
	    val x1 = ref nil
	    val x2 = ref nil
	in
	    Gen.try 100 (Gen.charArray Gen.char)
	      (Test.test (fn a => let
				    val t1 = CharArray.fromList 
						 (CharArray.toList a)
				    val t2 = CharArray.fromList 
						 (CharArray.toList a)
				      val t3 = CharArray.toList a
				  in
				      x1 := nil;
				      x2 := nil;
				      t (f x1) t1 0;
				      m (f x2) t2;
				      !x1 = !x2 andalso 
				      !x1 = List.rev t3 andalso
				      CharArray.equal Char.equal (t1, t2)
				  end))
	end

    fun testModify () = 
	let
	    fun f y x= (y := x :: !y; fromInt (Char.ord x + 5))

	    fun t x a i = if i = CharArray.length a
			      then ()
			  else (CharArray.update (a, i, 
						   x (CharArray.sub (a, i)));
				t x a (i + 1))
	in
	    mkTestModify t f CharArray.modify
	end

    fun testModifyi () =
	let
	    fun f y (i, x) = (y := x :: !y; fromInt (i + Char.ord x + 5))

	    fun t f a i = if i = CharArray.length a
			      then ()
			  else (CharArray.update (a, i, 
						 f (i, CharArray.sub (a, i)));
				t f a (i + 1))
	in
	    mkTestModify t f CharArray.modifyi
	end


    fun testFoldl () =
	let
	    val test1 = [0, 1, 2, 3]
	    val test1 = List.map fromInt test1
		
	    fun t f b   nil   = b
	      | t f b (x::xs) = t f (f (x, b)) xs

	    fun f (a, b) = b@[a]


	in
	    Test.test (fn xs => let
				   val a1 = CharArray.fromList xs
				   val a2 = CharArray.fromList xs
				in 
				    CharArray.foldl List.:: nil a1 =
				    List.rev xs
				end) test1;
	    Gen.try 100 (Gen.list Gen.char) 
	        (Test.test (fn xs => 
			    let
				val a = CharArray.fromList xs
			    in 
				CharArray.foldl f nil a =
				List.foldl f nil xs
			    end))
	end

    fun testFoldr () =
	let
	    val test1 = [0, 1, 2, 3]
	    val test1 = List.map fromInt test1
		
	    fun t f b   nil   = b
	      | t f b (x::xs) = t f (f (x, b)) xs


	in
	    Test.test (fn xs => let
				   val a1 = CharArray.fromList xs
				   val a2 = CharArray.fromList xs
				in 
				    CharArray.foldr List.:: nil a1 =
				    xs
				end) test1;
	    Gen.try 100 (Gen.list Gen.char) 
	        (Test.test (fn xs => 
			    let
				val a = CharArray.fromList xs
			    in 
				CharArray.foldr List.:: nil a =
				List.foldr List.:: nil xs
			    end))
	end

    fun testFoldli () =
	let
	    val test1 = [0, 1, 2, 3]
	    val test1 = List.map fromInt test1
		
	    fun t f b   nil   = b
	      | t f b (x::xs) = t f (f (x, b)) xs

	    fun f (i, a, b) = b@[fromInt (Char.ord a + i)]

	    fun f' (i, a, b) = a::b


	in
	    Test.test (fn xs => let
				   val a1 = CharArray.fromList xs
				   val a2 = CharArray.fromList xs
				in 
				    CharArray.foldli f' nil a1 =
				    List.rev xs
				end) test1;
	    Gen.try 100 (Gen.list Gen.char) 
	        (Test.test (fn xs => 
			    let
				val a = CharArray.fromList xs
			    in 
				CharArray.foldli f nil a =
				List.foldli f nil xs
			    end))
	end

    fun testFoldri () =
	let
	    val test1 = [0, 1, 2, 3]
	    val test1 = List.map fromInt test1
		
	    fun t f b   nil   = b
	      | t f b (x::xs) = t f (f (x, b)) xs

	    fun f (i, a, b) = b@[fromInt (Char.ord a + i)]

	    fun f' (i, a, b) = a::b


	in
	    Test.test (fn xs => let
				   val a1 = CharArray.fromList xs
				   val a2 = CharArray.fromList xs
				in 
				    CharArray.foldli f' nil a1 =
				    List.rev xs
				end) test1;
	    Gen.try 100 (Gen.list Gen.char) 
	        (Test.test (fn xs => 
			    let
				val a = CharArray.fromList xs
			    in 
				CharArray.foldli f nil a =
				List.foldli f nil xs
			    end))
	end

    fun testFoldri () =
	let
	    val test1 = [0, 1, 2, 3]
	    val test1 = List.map fromInt test1
		
	    fun t f b   nil   = b
	      | t f b (x::xs) = t f (f (x, b)) xs

	    fun f (i, a, b) = b@[fromInt (Char.ord a + i)]

	    fun f' (i, a, b) = a::b


	in
	    Test.test (fn xs => let
				   val a1 = CharArray.fromList xs
				   val a2 = CharArray.fromList xs
				in 
				    CharArray.foldli f' nil a1 =
				    List.rev xs
				end) test1;
	    Gen.try 100 (Gen.list Gen.char) 
	        (Test.test (fn xs => 
			    let
				val a = CharArray.fromList xs
			    in 
				CharArray.foldli f nil a =
				List.foldli f nil xs
			    end))
	end

    fun testFoldri () =
	let
	    val test1 = [0, 1, 2, 3]
	    val test1 = List.map fromInt test1
		
	    fun t f b   nil   = b
	      | t f b (x::xs) = t f (f (x, b)) xs

	    fun f (i, a, b) = (fromInt (Char.ord a + i)) :: b

	    fun f' (i, a, b) = a::b

	in
	    Test.test (fn xs => let
				   val a1 = CharArray.fromList xs
				   val a2 = CharArray.fromList xs
				in 
				    CharArray.foldri f' nil a1 =
				    xs
				end) test1;
	    Gen.try 100 (Gen.list Gen.char) 
	        (Test.test (fn xs => 
			    let
				val a = CharArray.fromList xs
			    in 
				CharArray.foldri f nil a =
				List.foldri f nil xs
			    end))
	end

    fun testApp () =
	let
	    val x1 = ref nil
	    val x2 = ref nil

	    fun f1 x = x1 := (x :: !x1)
	    fun f2 x = x2 := (x :: !x2)
	in
	    Gen.try 10 (Gen.charArray Gen.char)
	      (Test.test (fn a => (List.app f1 (CharArray.toList a);
				   CharArray.app f2 a;
				    let
					val b = !x1 = !x2
				    in 
					x1 := nil;
					x2 := nil; b
				    end)))
	end

    fun testAppr () =
	let
	    val x1 = ref nil
	    val x2 = ref nil

	    fun f1 x = x1 := (x :: !x1)
	    fun f2 x = x2 := (x :: !x2)
	in
	    Gen.try 10 (Gen.charArray Gen.char)
	      (Test.test (fn a => (List.appr f1 (CharArray.toList a);
				    CharArray.appr f2 a;
				    let
					val b = !x1 = !x2
				    in 
					x1 := nil;
					x2 := nil; b
				    end)))
	end

    fun testAppi () =
	let
	    val x1 = ref nil
	    val x2 = ref nil

	    fun f1 (i, x) = x1 := ((fromInt (i + Char.ord x)) :: !x1)
	    fun f2 (i, x) = x2 := ((fromInt (i + Char.ord x)) :: !x2)
	in
	    Gen.try 10 (Gen.charArray Gen.char)
	      (Test.test (fn a => (List.appi f1 (CharArray.toList a);
				   CharArray.appi f2 a;
				    let
					val b = !x1 = !x2
				    in 
					x1 := nil;
					x2 := nil; b
				    end)))
	end

    fun testAppri () =
	let
	    val x1 = ref nil
	    val x2 = ref nil

	    fun f1 (i, x) = x1 := ((fromInt (i + Char.ord x)) :: !x1)
	    fun f2 (i, x) = x2 := ((fromInt (i + Char.ord x)) :: !x2)
	in
	    Gen.try 10 (Gen.charArray Gen.char)
	      (Test.test (fn a => (List.appri f1 (CharArray.toList a);
				    CharArray.appri f2 a;
				    let
					val b = !x1 = !x2
				    in 
					x1 := nil;
					x2 := nil; b
				    end)))
	end

    val suite = ("CharArray", [("maxLen", testMaxLen),
			   ("sub", testSub),
			   ("update", testUpdate),
			   ("length", testLength),
			   ("toList", testToList),
			   ("array", testArray),
			   ("vector", testVector),
			   ("toVector", testToVector),
			   ("fromList", testFromList),
			   ("tabulate", testTabulate),
			   ("contains", testContains),
			   ("notContains", testNotContains),
			   ("sort", testSort),
			   ("isSorted", testIsSorted),
			   ("equal", testEqual),
			   ("collate", testCollate),
			   ("swap", testSwap),
			   ("rev", testRev),
			   ("copy", testCopy),
			   ("copyVec", testCopyVec),
			   ("all", testAll),
			   ("exists", testExists),
			   ("find", testFind),
			   ("alli", testAlli),
			   ("existsi", testExistsi),
			   ("findi", testFindi),
			   ("modify", testModify),
			   ("modifyi", testModifyi),
			   ("foldl", testFoldl),
			   ("foldr", testFoldr),
			   ("foldri", testFoldri),
			   ("foldli", testFoldli),
			   ("app", testApp),
			   ("appr", testAppr),
			   ("appi", testAppi),
			   ("appri", testAppri)])
end
