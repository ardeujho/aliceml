<?php include("macros.php3"); ?>
<?php heading("The PROBLEM signature", "The <TT>PROBLEM</TT> signature"); ?>

<P></P>
<?php section("synopsis", "synopsis"); ?>
  <PRE>
    signature PROBLEM</PRE>
   <P>A <TT>PROBLEM</TT> is a specification of a constraint problem, including recomputation policy,
   debug level, and branch and bound optimization. A structure with this signature can be passed as an argument
   to the <a href="sf-search.html"<TT>MkSearch</TT></A> functor.</P>
   <P>Have a look at the <A href="#examples">examples</A> below.</P>

  <P>
    See also:
    <A href="sf-debug.html"><TT>DEBUG</TT></A>,
    <A href="sf-search.html"><TT>SEARCH</TT></A>,
    <A href="sf-distributed.html"><TT>MkEngine</TT></A> (Distributed Search).
  </P>

<?php section("import", "import"); ?>

  <PRE>
    import signature PROBLEM from "x-alice:/lib/gecode/search-factory/PROBLEM-sig"</PRE>

<?php section("interface", "interface"); ?>
  <PRE>
signature PROBLEM =
sig
  type <A href="#solution">solution</A>
  type <A href="#space">space</A>

  val <A href="#root">root</A>         : space
  val <A href="#readSolution">readSolution</A> : space -> solution
  val <A href="#copyq">copyq</A>        : int -> bool
  val <A href="#mask">mask</A>         : Debug.db_mask

  val <A href="#bab">bab</A>          : bool
  val <A href="#bound">bound</A>        : space * solution -> unit
  val <A href="#compare">compare</A>      : solution * solution -> bool
       
end</PRE>

<?php section("description", "description"); ?>

  <DL>
    <DT>
      <TT>type <A name="solution">solution</A></TT>
    </DT>
    <DD>
      <P>The type of concrete solutions. Usually a tuple or a vector of integers.
      </P>
    </DD>

    <DT>
      <TT>type <A name="space">space</A></TT>
    </DT>
    <DD>
      <P>The type of constraint spaces. Always <A href="space.html"><TT>Space.space</TT></A>.
      </P>
    </DD>

    <DT>
      <TT><A name="root">root</A></TT>
    </DT>
    <DD><P>The root space, that is, a fresh constraint space where constraints have been posted.
           The search algorithm (see <A href="sf-search.html">SEARCH</A>) will use this space as a starting point.
        </P>
    </DD>

    <DT>
      <TT><A name="readSolution">readSolution</A> <I>space</I></TT>
    </DT>
    <DD><P>This function should extract a solution from a solved space (<TT>space</TT>).</P>
    </DD>

    <DT>
      <TT><A name="copyq">copyq</A> <I>depth</I></TT>
    </DT>
    <DD><P><I>Recomputation policy</I>: all the nodes of the search tree whose depth satisfies
        the predicate <TT>copyq depth = true</TT> will be copied and used for recomputation.
        As an example, a fixed distance recomputation policy can be defined using
        <TT>fun copyq d = d mod fixedDistance = 0</TT>, where <TT>fixedDistance</TT> is a given integer.
        Note that although this scheme is quite flexible, it is not possible to define real <I>adaptative</I>
        policies that way.
        </P>
    </DD>

    <DT>
      <TT><A name="mask">mask</A></TT>
    </DT>
    <DD><P>A debug mask that selects which debug messages are to be printed. See <A href="sf-debug.html">Debug</A>.</P>
    </DD>

    <DT>
      <TT><A name="bab">bab</A></TT>
    </DT>
    <DD><P>Should be set to true for Branch & Bound search. Otherwise (simple search), should be false.</P>
    </DD>

    <DT>
      <TT><A name="bound">bound</A> <I>(space, solution)</I></TT>
    </DT>
    <DD><P>Should bound the given space with the given solution.</P>
        <P><B>Required</B>: This function is called only if Branch & Bound is active (that is, <TT>bab = true</TT>).
           Otherwise, you may define it as <TT>fun bound _ = assert false</TT>.
       </P>
    </DD>

    <DT>
      <TT><A name="compare">compare</A> <I>(solution1, solution2)</I></TT>
    </DT>
    <DD><P>Should return true if and only if <TT><I>solution2</I></TT> is better than <TT><I></I>solution1</TT>.</P>
        <P><B>Required</B>: This function is called only if
           <A href="sf-search.html#init">Search.init</A> or <A href="sf-search.html#betterThan">Search.betterThan</A>
           are to be called. It is also used by distributed search with Branch & Bound active.
           In all other cases, including local search, and local Branch & Bound search, this function is <B>not</B>
           required and can be defined as <TT>fun compare _ = assert false</TT>.
        </P>
    </DD>
  </DL>

<?php section("examples", "examples"); ?>
<P>
<A name="examples"></A>
The first example is the legacy n-queen problem.
It uses a fixed distance recomputation policy.
It does not use Branch & Bound, thus the corresponding functions are
defined as <TT>assert false</TT>.
<PRE>
import structure Debug     from "x-alice:/lib/gecode/search-factory/Debug"
import structure MkSearch  from "x-alice:/lib/gecode/search-factory/MkSearch"
import structure Space     from "x-alice:/lib/gecode/Space"
import structure FD        from "x-alice:/lib/gecode/FD"
import structure Inspector from "x-alice:/lib/tools/Inspector"

(* The initial root space. *)
val root = Space.new () 
  
val size = 8

open FD
val cn = FD.BND
val v = rangeVec(root, size, (0, size-1))
val v1 = Vector.tabulate (size, fn i => (i, Vector.sub(v,i)))
val v2 = Vector.tabulate (size, fn i => (~i, Vector.sub(v,i)))

val _ =
    (distinctOffset(root, v1, cn);
     distinctOffset(root, v2, cn);
     distinct(root, v, cn);
     branch(root, v, B_SIZE_MIN, B_MIN))

fun toInt space v = FD.Reflect.value (space,v)
fun readSolution space = Vector.map (fn var => FD.Reflect.value (space, var)) v

(* Recomputation Policy : fixed distance *)
val rdist = 3

structure Problem =
struct
  type solution = int Vector.t
  type space = Space.space

  val root = root
  val readSolution = readSolution
  fun copyq d = d mod rdist = 0

  val bab = false
  fun bound _ = assert false
  fun compare _ = assert false

  val mask = Debug.dbNone
end

structure Search = MkSearch Problem

val solutions = Search.getAllSolutions ()

val _ = Inspector.inspect solutions
</PRE>
</P>

<P>
The second example can be described as follows:
given two tuples of n integers, written A<SUB>i</SUB> and B<SUB>i</SUB>, create a
new tuple C<SUB>i</SUB> of distinct integers such that for all i, C<SUB>i</SUB> is
either A<SUB>i</SUB> or B<SUB>i</SUB>.
A score is associated to each solution by computing the sum of the
integers of the tuple. Greatest score is best.
<PRE>
import structure Debug     from "x-alice:/lib/gecode/search-factory/Debug"
import structure MkSearch  from "x-alice:/lib/gecode/search-factory/MkSearch"
import structure Space     from "x-alice:/lib/gecode/Space"
import structure FD        from "x-alice:/lib/gecode/FD"
import structure Inspector from "x-alice:/lib/tools/Inspector"

(* The initial root space. *)
val root = Space.new () 

(*** Search problem :
 *   Choose one number in each column (numbers1, numbers2)
 *   All numbers must be different
 *)
val max = 10
val size = 6
val numbers1 = #[2, 1, 2, 5, 1, 6]
val numbers2 = #[1, 3, 4, 3, 6, 7]

infix %
fun a % b = Vector.sub (a,b)

val cn = FD.BND
 
fun fromInt sp n = FD.intvar (sp,#[(n,n)])
fun toInt   sp v = FD.Reflect.value (sp,v)
  
val vars  = FD.rangeVec   (root, size, (0, max))
val reif  = FD.boolvarVec (root, size)
val nreif = FD.boolvarVec (root, size) (* means "logical-not of reif" *)
val reif2 = Vector.map FD.boolvar2intvar reif
val sum   = FD.intvar (root, #[(0, size*max)])
val kvars = Vector.tabulate
		(size+1, (fn i => if i<size then (1,vars%i) else (~1, sum)))
fun readSolution space = Vector.map (toInt space) vars
fun bound (space, sol) =
    let
      val lsum = Vector.foldl (fn (e,s) => e+s) 0 sol
      val vsum = fromInt space lsum
    in
      FD.rel (space, sum, FD.GR, vsum)
    end
	
val _ =
    (* Propagators. *)
    (FD.distinct (root, vars, cn) ;
     VectorPair.app
       (fn (b1, b2) => FD.nega(root, b1, b2)) (reif, nreif) ;
     
     Vector.appi
       (fn (i, var) =>
	   (FD.Reified.rel
	     (root, var, FD.EQ, fromInt root (numbers1%i), reif%i) ;
	     FD.Reified.rel
	       (root, var, FD.EQ, fromInt root (numbers2%i), nreif%i)))
       vars ;
	 
	 (* Sum *)
	 FD.linear (root, kvars, FD.EQ, 0, cn) ;
	 
         (* Branching policy *)
	 FD.branch (root, reif2, FD.B_NONE, FD.B_MIN))
  
(* Recomputation Policy : fixed distance *)
val rdist = 3

structure Problem =
struct
  type solution = int Vector.t
  type space = Space.space

  val root = root
  val readSolution = readSolution
  fun copyq d = d mod rdist = 0
  val bab = true
  val bound = bound

  fun compare _ = assert false
  val mask = Debug.dbNone
end

structure Search = MkSearch Problem

val solutions = Search.getAllSolutions ()

val _ = Inspector.inspect solutions
</PRE>
</P>  

<?php footing(); ?>
